resource "proxmox_virtual_environment_vm" "vm" {
  name        = var.name
  description = "Talos VM for ${var.name} (Generated by TF)"

  node_name = var.node

  cpu {
    cores = var.cpus
    type  = "host" # "x86-64-v2-AES"
  }

  memory {
    dedicated = var.memory
    floating  = var.memory
  }

  scsi_hardware = "virtio-scsi-single"

  agent {
    enabled = true
  }

  boot_order = [
    "scsi0", # Main disk for OS
    "ide2",  # Secondary Master (CD-ROM)
    "net0"   # Network boot
  ]

  serial_device {}

  network_device {
    bridge   = "vmbr0"
    firewall = false
    vlan_id  = var.vlan_tag
    model    = "virtio"
  }

  network_device {
    bridge   = "vmbr0"
    firewall = false
    vlan_id  = var.ceph_vlan_tag
    model    = "virtio"
  }

  disk {
    datastore_id = "vmdata"
    file_format  = "raw"
    interface    = "scsi0"
    size         = var.disk_size
  }

  cdrom {
    file_id   = var.iso
    interface = "ide2"
  }
}

resource "unifi_user" "vm_ip" {
  mac  = tolist(proxmox_virtual_environment_vm.vm.network_device)[0].mac_address
  name = var.name
  note = "IP for VM: ${var.name} (Generated by TF)"

  fixed_ip   = "10.1.21.1${var.node_index + 1}${var.index + 1}"
  network_id = var.network_id
}

output "vm_ip" {
  value = unifi_user.vm_ip.fixed_ip
}
